plugins {
    id 'com.android.application'
}

apply from: "$project.rootDir/spotless.gradle"

android {
    namespace 'rocks.tbog.livewallpaperit'
    compileSdk compile_sdk_ver

    buildFeatures {
        buildConfig true
        compose true
    }

    defaultConfig {
        applicationId "rocks.tbog.livewallpaperit"
        minSdk min_sdk_ver
        targetSdk compile_sdk_ver
        multiDexEnabled true
        versionCode 2
        versionName "1.0"

        def lwiAuthorityValue = "rocks.tbog.livewallpaperit"
        manifestPlaceholders["lwiAuthority"] = lwiAuthorityValue
        buildConfigField "String", "LWI_AUTHORITY", "\"${lwiAuthorityValue}\""
        buildConfigField "String", "MUZEI_PACKAGE_NAME", "\"net.nurik.roman.muzei\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["store"]
    productFlavors {
        playstore {
            dimension "store"
            buildConfigField 'boolean', 'SHOW_PRIVACY_POLICY', 'true'
            buildConfigField 'boolean', 'SHOW_RATE_APP', 'true'
        }
        fdroid {
            dimension "store"
            buildConfigField 'boolean', 'SHOW_PRIVACY_POLICY', 'false'
            buildConfigField 'boolean', 'SHOW_RATE_APP', 'true'
        }
        github {
            dimension "store"
            buildConfigField 'boolean', 'SHOW_PRIVACY_POLICY', 'false'
            buildConfigField 'boolean', 'SHOW_RATE_APP', 'false'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility java_target
        targetCompatibility java_target
    }
}

dependencies {

    implementation "androidx.appcompat:appcompat:$ax_appcompat_ver"
    implementation "com.google.android.material:material:$material_ver"
    implementation "androidx.constraintlayout:constraintlayout:$ax_constraintlayout_ver"
    implementation "com.google.android.apps.muzei:muzei-api:$muzei_api_ver"
    implementation "androidx.lifecycle:lifecycle-extensions:$ax_lifecycle_ver"
    implementation "androidx.work:work-runtime:$ax_work_ver"
    implementation "androidx.preference:preference:$ax_preference_ver"
    implementation project(path: ':ARAW')
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$desugar_ver"
}
